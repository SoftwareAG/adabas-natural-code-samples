* Sample CHECKSUM to compare contents of two data base files
*   - same file on two databases
*
* Customization:
*   1 Provide DBIDs
*   2 Provide file name in #FILE
*   3 Provide file name in VIEW
*   4 Provide a full DDM definition following VIEW
*   5 Provide LRECL in #DDM - try (A50000) - Natural will correct you
*   6 Following testing, remove the READ limit of (10)
*   7 Specify a unique READ key - records must be in the same sequence
*     . ISN or descriptor
*   8 For cross-platform comparisons, ensure a consistent sign nibble
*     .     ensure identical PSIGNF parameter
*       or  insert "ADD 0" for each packed field
*
DEFINE DATA LOCAL
1 #DB1 (N5)        INIT <001>               /* <<<<< 1
1 #DB2 (N5)        INIT <002>               /* <<<<< 1
1 #FILE (A32)      INIT <"EMPLOYEES">       /* <<<<< 2
1 DDM   VIEW EMPLOYEES                      /* <<<<< 3
  2 PERSONNEL-ID                            /* <<<<< 4
  2 FIRST-NAME
  2 MIDDLE-I
  2 NAME
  2 MIDDLE-NAME
  2 MAR-STAT
  2 SEX
  2 BIRTH
  2 ADDRESS-LINE (5)
  2 CITY
  2 ZIP
  2 POST-CODE
  2 COUNTRY
  2 AREA-CODE
  2 PHONE
  2 DEPT
  2 JOB-TITLE
  2 CURR-CODE (10)
  2 SALARY (10)
  2 BONUS (10, 10)
  2 LEAVE-DUE
  2 LEAVE-TAKEN
  2 LEAVE-START (20)
  2 LEAVE-END (20)
  2 LANG (5)
                   1 REDEFINE DDM
  2 #DDM (A1189)                            /* <<<<< 5
1 #1023
  2 FUNC (A1)      INIT <"M">
  2 RC (N4)
  2 TIME (T)
  2 DATE (D)
  2 TS (B8)
  2 MS (P19)
1 #1040
  2 OP (A1)        INIT <'S'>
  2 DB (N5)
  2 RC (I4)
1 #4011
  2 FUNC (I4)
  2 CTX (B156)
  2 TEXT (A) DYNAMIC
  2 HASH (B20)
1 #HASH (B20)
1 #COUNT (P10)
1 #ELAPSED (N7)
END-DEFINE
*
FORMAT SG=F
*
DEFINE SUBROUTINE HASH-RTN
CALLNAT "USR1040N" #1040
ASSIGN #4011.FUNC = 1
CALLNAT "USR4011N" #4011
ASSIGN #4011.FUNC = 2
ASSIGN #1023.MS   = *CPU-TIME
E.
SET TIME
R.
READ (10) MULTI-FETCH ON                    /* <<<<< 6
                         DDM BY ISN         /* <<<<< 7
  ADD 0 TO DDM.BIRTH                        /* <<<<< 8
  ADD 0 TO DDM.SALARY (*)                   /* <<<<< 8
  ADD 0 TO DDM.BONUS  (*, *)                /* <<<<< 8
  ASSIGN #4011.TEXT = #DDM
  CALLNAT "USR4011N" #4011
END-READ
ASSIGN #4011.FUNC = 3
CALLNAT "USR4011N" #4011
ASSIGN #1023.MS = *CPU-TIME - #1023.MS
CALLNAT "USR1023N" #1023
ASSIGN #ELAPSED = *TIMD (E.)
DISPLAY 'DBID'     #1040.DB
        'Records'  *COUNTER (R.)
        'Checksum' #4011.HASH
        'CPU Time' #1023.TIME (EM=HH:II:SS.T)
        'Elapsed'  #ELAPSED (EM=99':'99':'99'.'9)
END-SUBROUTINE
*
*
ASSIGN #1040.DB = #DB1
PERFORM HASH-RTN
ASSIGN #HASH  = #4011.HASH
ASSIGN #COUNT = *COUNTER (R.)
*
ASSIGN #1040.DB = #DB2
PERFORM HASH-RTN
*
IF  *COUNTER (R.) = #COUNT
  THEN
    WRITE / T**COUNTER (R.) 'Counters match' (GRI)
  ELSE
    WRITE / T**COUNTER (R.) '>>>>>  Counters do not match  <<<<<' (REI)
END-IF
IF  #4011.HASH = #HASH
  THEN
    WRITE / T*#4011.HASH 'Checksums match' (GRI)
  ELSE
    WRITE / T*#4011.HASH '>>>>>  Checksums do not match  <<<<<' (REI)
END-IF
*
WRITE TITLE LEFT
          *PROGRAM
      10T 'Checksum Comparison'
      62T *DAT4U
          *TIME (AL=5)
    / 10T 'File' #FILE
    /
END